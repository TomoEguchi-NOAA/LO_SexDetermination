#Model_sex_tail_SCL.txt

# Tomo Eguchi
# 24 February 2015
# 27 September 2019 converted for ETP Lo analysis
# 5 January 2021 added SCL to sex_tail
# 28 February 2022 Converted SCL to temp
# 28 February 2022 Added SCL

model {
  for (i in 1:N1){
      for (j in 1:2){
        # variability between two samples are about 0.01 (= variance)
        # so even precision = 10 (= var = 0.1) is an order of magnitude
        # greater than the observed - should be "flat" enough - see sigma_H1
        # below.

        H1[i, j] ~ dnorm(mu_H1[i], tau_H1)
        loglik[i, j] <- logdensity.norm(H1[i,j], mu_H1[i], tau_H1)
      }
      
      mu_H1[i] <- beta_0 + beta_Sex * Sex1[i] + beta_T * tail[i] + beta_L * SCL[i] + beta_temp * temp[i]
      
  }

  # because we don't have all sex identifications
  for (i in 1:N1){
    Sex1[i] ~ dbern(q)
  }

  q ~ dbeta(1,1)

  # Setting SD of observations to be somewhere between 0 to 0,4, which equates
  # to var = 0 - 0.16, which is an order of magnitude greater than the observed
  # variance ~ 0.01
  sigma_H1 ~ dunif(0, 0.4) #1/sqrt(tau_H1)

  tau_H1 <- 1/(sigma_H1^2) 
  
  beta_0 ~ dnorm(0, 0.1)
  beta_Sex ~ dnorm(0, 0.1)
  beta_T ~ dnorm(0, 0.1)
  beta_L ~ dnorm(0, 0.1)
  beta_temp ~ dnorm(0, 0.1)
}